grammar:
    _ rules _

rules:
    rules __ rule
    | rule

rule:
    id _ ":" _ choice

choice:
    choice _ "|" _ sequence
    | sequence

sequence:
    sequence __ term
    | term

term:
    target _ primary _ multiplicity

target:
    id _ assignment_operator
    | %

assignment_operator:
    "="
    | "+" "="

multiplicity:
    "?" | "*" | "+" | %

primary:
    id
    | string
    | ranges
    | "(" choice ")"

id:
    id [a-zA-Z0-9_]
    | [a-zA-Z0-9_]

string:
    "\"" string_content "\""

string_content:
    string_content string_char
    | string_char

string_char:
    [^"\\]
    | "\\" .

ranges:
    ranges _ range
    | range

range:
    "[" range_content "]"
    | "."

range_content:
    range_content range_char
    | range_char

range_char:
    [^\]\\]
    | "\\" .

__:
    [ \r\n\t] _
    | comment _

_:
    __ | %

comment:
    "/" "/" single_line_comment_content [\r\n]
    | "/" "*" multiline_comment_content multiline_comment_end

single_line_comment_content:
    single_line_comment_content [^\r\n]
    | %

multiline_comment_content:
    multiline_comment_content [^*]
    | multiline_comment_content "*" [^*/]
    | %

multiline_comment_end:
    multiline_comment_end "*"
    | "*" "/"
